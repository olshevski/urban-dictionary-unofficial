apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "dev.olshevski.udu"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "0.9.9"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "BUILD_DATE", "\"${new Date().format('MMM dd, yyyy')}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard-rules.pro'
            proguardFile 'dontobfuscate.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"
        freeCompilerArgs += "-Xopt-in=androidx.paging.ExperimentalPagingApi"
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    // kotlinx
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'

    // androidx
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle"
    implementation "androidx.paging:paging-runtime:3.0.0-alpha05"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"
    implementation "androidx.savedstate:savedstate:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.2.0-alpha05"
    implementation "androidx.browser:browser:1.2.0"

    // network
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.moshi:moshi:$versions.moshi"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'

    // tools
    implementation 'org.koin:koin-androidx-viewmodel:2.1.5'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'

    // tests
    testImplementation "io.kotest:kotest-runner-junit5-jvm:$versions.kotest"
    testImplementation "io.kotest:kotest-assertions-core-jvm:$versions.kotest"

    // instrumentation tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

fileTree(include: ['*.gradle'], dir: 'gradle-plugins').each { apply from: it }